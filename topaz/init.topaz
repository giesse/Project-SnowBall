; Topaz Interpreter Initialization

make: conjure 'make
native!: conjure 'native!
word!: conjure 'word!
datatypes: conjure 'datatypes

foreach: make native! [[words series body] foreach]
set: make native! [[word value] set]

foreach 'type datatypes [
    set make word! type type
]

reduce: make native! [[block] reduce]

foreach [native spec] [
    insert            [series value]
    head              [series]
    pick              [series pos]
    length-of         [series]
    skip              [series amount]
    mold              [value indent]
    do                [block]
    get               [word]
    bind              [words context]
    tail              [series]
    print             [string]
    prin              [string]
    load              [string]
    read              [filename]
    try               [code word default]
    add               [val1 val2]
    subtract          [val1 val2]
    equal?            [val1 val2]
    greater?          [val1 val2]
    lesser?           [val1 val2]
    not-equal?        [val1 val2]
    greater-or-equal? [val1 val2]
    lesser-or-equal?  [val1 val2]
    compile           [block]
    write             [filename text]
    throw             [string]
    if                [condition body]
    either            [condition true-body false-body]
    not               [value]
    apply             [func args]
    while             [condblock body]
    all               [block]
    any               [block]
    switch            [value cases default]
    case              [cases]
    context-of        [word]
    rejoin            [block]
    type-of           [value]
    find              [series value]
    until             [body]
    copy              [series]
    cause             [error]
    catch             [code]
    form-error        [error]
    in?               [container value]
    ; compiler only
    function          [spec locals body]
    make-struct       [spec]
    make-array        []
    poke-array        [array position value]
    pick-array        [array position]
    regexp            [exp flags]
    to-char           [number]
    js-error?         [value]
] [
    set native make native! reduce [spec native]
]

foreach [op f] [
    +  add
    -  subtract
    =  equal?
    >  greater?
    <  lesser?
    <> not-equal?
    >= greater-or-equal?
    <= lesser-or-equal?
] [
    set op make op! get f
]

none: make none! 0
true: on: yes: 0 = 0
false: off: no: 0 = 1

any-word!: make typeset! [word! get-word! set-word! lit-word!]
any-path!: make typeset! [path! set-path! lit-path!]
any-block!: make typeset! [any-path! block! paren!]
any-function!: make typeset! [function! native!]
series!: make typeset! [any-block! string!]

func: make function! [[spec body] [make function! reduce [spec body]]]

in: make op! func [value container] [in? container value]
first: func [series] [pick series 0]
second: func [series] [pick series 1]
empty?: func [series] [0 = length-of series]
mold*: :mold
mold: func [value] [mold* :value ""]
probe: func [value] [
    print mold :value
    :value
]
append: func [series value] [
    head either block? :value [
        series: tail series
        foreach 'val value [
            series: insert series :val
        ]
        series
    ] [
        insert tail series :value
    ]
]
context: func [code] [make context! code]
object: func [parent code] [make object! reduce [parent code]]
next: func [series] [skip series 1]
set-word?: func [value] [set-word! = type-of :value]
word?: func [value] [word! = type-of :value]
block?: func [value] [block! = type-of :value]
found?: func [series] [all [series not empty? series]]
any-word?: func [value] [in? any-word! type-of :value]
any-path?: func [value] [in? any-path! type-of :value]
any-block?: func [value] [in? any-block! type-of :value]
any-function?: func [value] [in? any-function! type-of :value]

system: object none [
    name: "Topaz"
    version: "alpha"
    words: context-of 'system
    datatypes: datatypes
]

parse-match: func [cursor condition] [
    either all [not empty? cursor/block do condition] [
        context [
            block: next cursor/block
            rules: next cursor/rules
            match?: yes
            result: first cursor/block
        ]
    ] [
        context [
            block: cursor/block
            rules: next cursor/rules
            match?: no
            result: none
        ]
    ]
]
parse-any: func [rule cursor] [
    res-value: none
    until [
        res: parse-element rule cursor
        if res/match? [res-value: res/result]
        cursor/block: res/block
        not res/match?
    ]
    context [
        block: res/block
        rules: res/rules
        match?: yes
        result: res-value
    ]
]
parse-element: func [rule cursor] [
    switch make word! type-of rule [
        word! [
            switch rule [
                any [
                    parse-any second cursor/rules context [
                        block: cursor/block
                        rules: next cursor/rules
                        collect: cursor/collect
                    ]
                ]
                some [
                    result: parse-element second cursor/rules context [
                        block: cursor/block
                        rules: next cursor/rules
                        collect: cursor/collect
                    ]
                    if result/match? [
                        result: parse-any second cursor/rules context [
                            block: result/block
                            rules: next cursor/rules
                            collect: cursor/collect
                        ]
                    ]
                    result
                ]
                skip * [
                    context [
                        block: next cursor/block
                        rules: next cursor/rules
                        match?: not empty? cursor/block
                        result: if match? [first cursor/block]
                    ]
                ]
                end [
                    context [
                        block: cursor/block
                        rules: next cursor/rules
                        match?: empty? cursor/block
                        result: none
                    ]
                ]
                here [
                    context [
                        block: cursor/block
                        rules: next cursor/rules
                        match?: yes
                        result: cursor/block
                    ]
                ]
                into [
                    rule: second cursor/rules
                    if word? rule [rule: get rule]
                    if not block? rule [throw "Invalid argument for INTO"]
                    either all [not empty? cursor/block block? first cursor/block] [
                        res: parse* context [
                            block: first cursor/block
                            rules: rule
                            collect: cursor/collect
                        ]
                        context [
                            block: either res/match? [next cursor/block] [cursor/block]
                            rules: skip cursor/rules 2
                            match?: res/match?
                            result: res/result
                        ]
                    ] [
                        context [
                            block: cursor/block
                            rules: skip cursor/rules 2
                            match?: no
                            result: none
                        ]
                    ]
                ]
            ] [
                ; problem: will always recurse on word! - desirable?
                parse-element get rule cursor
            ]
        ]
        lit-word! [
            parse-match cursor [rule = first cursor/block]
        ]
        paren! [
            context [
                block: cursor/block
                rules: next cursor/rules
                match?: yes
                result: do rule
            ]
        ]
        block! [
            parse* context [
                block: cursor/block
                rules: rule
                collect: cursor/collect
            ]
        ]
        datatype! [
            parse-match cursor [rule = type-of first cursor/block]
        ]
    ] [
        parse-match cursor [rule = first cursor/block]
    ]
]
parse-step: func [rule cursor] [
    switch make word! type-of rule [
        word! [
            switch rule [
                collect [
                    res: parse-element second cursor/rules cur: context [
                        block: cursor/block
                        rules: next cursor/rules
                        collect: make block! none
                    ]
                    context [
                        block: res/block
                        rules: res/rules
                        match?: res/match?
                        result: cur/collect
                    ]
                ]
                ;collect-all [
                ;    handle-collect cursor cursor/keep-all? parse-element second cursor/rules context [
                ;        block: cursor/block
                ;        rules: next cursor/rules
                ;        collect: make block! none
                ;        keep-all?: yes
                ;    ]
                ;]
                copy [
                    res: parse-element second cursor/rules context [
                        block: cursor/block
                        rules: next cursor/rules
                        collect: none
                    ]
                    context [
                        block: res/block
                        rules: res/rules
                        match?: res/match?
                        result: copy-part cursor/block res/block
                    ]
                ]
                keep [
                    result: parse-step second cursor/rules context [
                        block: cursor/block
                        rules: next cursor/rules
                        collect: none
                    ]
                    if all [result/match? cursor/collect] [insert tail cursor/collect result/result]
                    result
                ]
                ;object [parse-object block next rules]
            ] [
                parse-element rule cursor
            ]
        ]
        set-word! [
            result: parse-step second cursor/rules context [
                block: cursor/block
                rules: next cursor/rules
                collect: cursor/collect
            ]
            if result/match? [set rule result/result]
            result
        ]
    ] [
        parse-element rule cursor
    ]
]
check-alternative: func [cursor result] [
    ; WARNING: cursor/collect is not restored
    if not result/match? [
        result/block: cursor/block
        result/rules: next find result/rules '|
    ]
    result
]
; parse block according to rule, keep last result
parse*: func [cursor] [
    result: context [
        block: cursor/block
        rules: cursor/rules
        match?: no
        result: none
    ]
    while [all [not empty? result/rules '| <> first result/rules]] [
        result: check-alternative cursor parse-step first result/rules context [
            block: result/block
            rules: result/rules
            collect: cursor/collect
        ]
    ]
    result
]
; parse block according to rule, return last result
parse: func [block' rules'] [
    cursor: parse* context [
        block: block'
        rules: rules'
        collect: none
    ]
    if cursor/match? [cursor/result]
]

; always deep
compose: func [block] [
    parse block rule: [
        collect any [
            code: paren! keep (do code)
            |
            keep into rule
            |
            keep *
        ]
    ]
]

compiler-keywords: context [
    none: make native! [[] none]
    true: make native! [[] true]
    false: make native! [[] false]
]
