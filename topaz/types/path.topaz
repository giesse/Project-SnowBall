; Topaz types: PATH!
;
; Copyright 2011 Gabriele Santilli
;
; Permission is hereby granted, free of charge, to any person obtaining
; a copy of this software and associated documentation files
; (the "Software"), to deal in the Software without restriction, including
; without limitation the rights to use, copy, modify, merge, publish,
; distribute, sublicense, and/or sell copies of the Software, and to
; permit persons to whom the Software is furnished to do so, subject
; to the following conditions:
;
; The above copyright notice and this permission notice shall be included
; in all copies or substantial portions of the Software.
;
; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
; OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
; ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
; OTHER DEALINGS IN THE SOFTWARE.

; ===== TYPES: PATH! ==========================================================

path!: make-type "path!" [
    make: function [arguments] [] [
        make-struct [
            type: path!
            values: make-array
            pos: 0
        ]
    ]
    topaz-make: function [ignored] [] [
        apply 'path!/make [none]
    ]
    insert: function [block value] [] [
        insert-array block/values block/pos value
        make-struct [
            type: path!
            values: block/values
            pos: block/pos + 1
        ]
    ]
    head: function [block] [] [
        make-struct [
            type: path!
            values: block/values
            pos: 0
        ]
    ]
    tail: function [block] [] [
        make-struct [
            type: path!
            values: block/values
            pos: length-of-array block/values
        ]
    ]
    skip: function [block amount] [] [
        make-struct [
            type: path!
            values: block/values
            pos: block/pos + amount
        ]
    ]
    pick: function [block pos] [] [
        pick-array block/values block/pos + pos
    ]
    length-of: function [block] [] [
        (length-of-array block/values) - block/pos
    ]
    mold: function [block indent] [result pos] [
        result: ""
        pos: block/pos
        if pos < length-of-array block/values [
            result: mold-step1 result block/values pos indent ""
            pos: pos + 1
            while [pos < length-of-array block/values] [
                result: mold-step1 result block/values pos indent #"/"
                pos: pos + 1
            ]
        ]
        result
    ]
    do: function [path block] [value] [
        if empty? path [error "Internal" "Empty PATH! value" mold-value block ""]
        value: get first path
        path: skip path 1
        while [not empty? path] [
            value: get-path value first path
            path: skip path 1
        ]
        either any [value/type/name = "function!" value/type/name = "native!"] [
            apply 'value/type/do [value block]
        ] [
            reduce [value skip block 1]
        ]
    ]
    bind: function [path context extend?] [] [
        bind-values path/values 0 context extend?
        path
    ]
    compile: function [path block] [expr] [
        if empty? path [error "Internal" "Empty PATH! value" mold-value block ""]
        expr: ast-get first path
        path: skip path 1
        while [not empty? path] [
            expr: ast-get-path expr first path
            path: skip path 1
        ]
        reduce [expr skip block 1]
    ]
    equal?: function [path value] [] [false]
    copy: function [value] [] [
        make-struct [
            type: path!
            values: apply 'value/values/slice [value/pos]
            pos: 0
        ]
    ]
]
path!/("path!"): make-struct [
    equal?: :compare-blocks
]
path!/("set-path!"): make-struct [
    equal?: :compare-blocks
]
path!/("lit-path!"): make-struct [
    equal?: :compare-blocks
]

parse-path: function [text] [path value] [
    if all [value: parse-word-chars text  #"/" = pick text length-of-array value] [
        path: make path! none
        path: insert path make word! value
        text: skip text length-of-array value
        while [all [not empty? text  #"/" = first text]] [
            set [value text] parse-path-element skip text 1
            either value [
                path: insert path value
            ] [
                error "Syntax" "Parse error" to-js-string text
            ]
        ]
        reduce [head path text]
    ]
]
