; Topaz types: OP!
;
; Copyright 2011 Gabriele Santilli
;
; Permission is hereby granted, free of charge, to any person obtaining
; a copy of this software and associated documentation files
; (the "Software"), to deal in the Software without restriction, including
; without limitation the rights to use, copy, modify, merge, publish,
; distribute, sublicense, and/or sell copies of the Software, and to
; permit persons to whom the Software is furnished to do so, subject
; to the following conditions:
;
; The above copyright notice and this permission notice shall be included
; in all copies or substantial portions of the Software.
;
; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
; OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
; ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
; OTHER DEALINGS IN THE SOFTWARE.

; ===== TYPES: OP! ============================================================

op!: make-type "op!" [
    make: function [arguments] [] [
        if 2 <> length-of arguments/spec [
            error make-struct [
                category: "Script"
                id: "invalid-argument"
                message: "Function must take exactly two arguments"
            ]
        ]
        make-struct [
            type: op!
            func: arguments
        ]
    ]
    topaz-make: function [func] [] [
        switch func/type/name [
            "function!" [apply 'op!/make [func]]
            "native!" [apply 'op!/make [func]]
        ] [
            error make-struct [
                category: "Script"
                id: "invalid-argument"
                message: "Invalid argument for MAKE OP!"
                args: func
            ]
        ]
    ]
    mold: function [op indent] [] [
        "make op! " + mold-value op/func indent
    ]
    do: function [op block] [] [
        error make-struct [
            category: "Script"
            id: "invalid-infix"
            message: "Infix use of OP!s is not supported"
        ]
    ]
    bind: function [op context extend?] [] [op]
    compile: function [op block] [] [
        error make-struct [
            category: "Compilation"
            id: "invalid-infix"
            message: "Infix use of OP!s is not supported"
        ]
    ]
    equal?: :default-equal?
]

operator?: function [block] [value] [
    all [
        not empty? block
        value: first block
        value/type/name = "word!"
        value: get value
        value/type/name = "op!"
        value
    ]
]
do-op: function [op arg1 arg2] [result] [
    switch op/func/type/name [
        "function!" [
            push-context op/func/context
            op/func/context/values: clone-values op/func/context
            poke-array op/func/context/values 0 arg1
            poke-array op/func/context/values 1 arg2
            result: do op/func/body
            pop-context op/func/context
            result
        ]
        "native!" [
            apply 'op/func/func [arg1 arg2]
        ]
    ] []
]
