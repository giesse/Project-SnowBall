; Topaz types: OBJECT!
;
; Copyright 2011 Gabriele Santilli
;
; Permission is hereby granted, free of charge, to any person obtaining
; a copy of this software and associated documentation files
; (the "Software"), to deal in the Software without restriction, including
; without limitation the rights to use, copy, modify, merge, publish,
; distribute, sublicense, and/or sell copies of the Software, and to
; permit persons to whom the Software is furnished to do so, subject
; to the following conditions:
;
; The above copyright notice and this permission notice shall be included
; in all copies or substantial portions of the Software.
;
; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
; OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
; ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
; OTHER DEALINGS IN THE SOFTWARE.

; ===== TYPES: OBJECT! ========================================================

object!: make-type "object!" [
    make: function [arguments] [] [
        make-struct [
            type: object!
            words: make-array
            map: make-struct []
        ]
    ]
    topaz-make: function [code] [obj] [
        obj: make object! none
        switch code/type/name [
            "block!" [
                parse-object-spec obj code
            ]
        ] [
            error "Script" "Invalid argument for MAKE OBJECT!" mold-value code ""
        ]
        obj
    ]
    mold: function [obj indent] [block i] [
        block: make block! none
        foreach 'word obj/words [
            block: insert insert-nl block make set-word! word true obj/map/:word
        ]
        new-line block true
        rejoin ["object " mold-value head block indent]
    ]
    do: function [object block] [] [
        reduce [object skip block 1]
    ]
    bind: function [value context extend?] [] [value]
    compile: function [object block] [] [
        reduce [ast-value object skip block 1]
    ]
    get-path: function [object selector] [] [
        if all [
            selector/type/name <> "word!"
            selector/type/name <> "lit-word!"
            selector/type/name <> "set-word!"
            selector/type/name <> "get-word!"
        ] [
            error "Script" "Invalid path value" mold-value selector ""
        ]
        any [object/map/(selector/word) make none! none]
    ]
    set-path: function [object selector set-to] [] [
        if all [
            selector/type/name <> "word!"
            selector/type/name <> "lit-word!"
            selector/type/name <> "set-word!"
            selector/type/name <> "get-word!"
        ] [
            error "Script" "Invalid path value" mold-value selector ""
        ]
        set-word-in-object object selector/word set-to
    ]
    equal?: :default-equal?
]

in-object?: function [obj word] [] [
    0 <= apply 'obj/words/index-of [word]
]

set-word-in-object: function [object word set-to] [] [
    if not in-object? object word [append-array object/words word]
    object/map/(word): set-to
]

parse-object-spec: function [obj spec] [word value] [
    while [not empty? spec] [
        word: first spec
        switch word/type/name [
            "set-word!" [
                set [value spec] do-step second spec next spec
                set-word-in-object obj word/word value
            ]
            "word!" [
                spec: next spec
                set-word-in-object obj word/word make logic! true
            ]
        ] [
            error "Script" "Invalid object spec" mold-value spec ""
        ]
    ]
    obj
]
